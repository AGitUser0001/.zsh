eval $_default_setup

local curcontext=":*" git=( (../)#.git(N) )
if [[ -n $git ]] {
  CURRENT_GIT_BRANCH=$(git rev-parse --abbrev-ref HEAD 2> /dev/null)
} else {
  CURRENT_GIT_BRANCH=''
}

[[ $CURRENT_GIT_BRANCH == HEAD ]] && CURRENT_GIT_BRANCH=$(git describe --all --exact-match HEAD 2> /dev/null)

if [[ $CURRENT_GIT_BRANCH ]] {
  local fgcolor bgcolor branch symbol
  curcontext=":git:${(b)CURRENT_GIT_BRANCH}"
  git diff --quiet &>/dev/null
  if (( $? )) { curcontext="$curcontext:dirty"
  } else { curcontext="$curcontext:clean" }
  curcontext="${curcontext}:*"

  zstyle -s ":prompt:X$curcontext:"        fgcolor   fgcolor || fgcolor=white
  zstyle -s ":prompt:X$curcontext:"        bgcolor   bgcolor || bgcolor=238
  zstyle -s ":prompt:X$curcontext:"        symbol    symbol  || symbol=''
  zstyle -s ":prompt:X$curcontext:branch"  icon      branch  || branch='î‚ '

  psvar[1,5]=( "$bgcolor" "$fgcolor" "${branch:+$branch }" "$CURRENT_GIT_BRANCH" "$symbol" )

  curcontext=":*"
  unset fgcolor bgcolor branch symbol
} else { psvar[1,5]=( "" "" "" "" "" ) }

local fgcolor bgcolor
local -F 1 diff=$_elapsed[-1]
curcontext=":time"
curcontext="$curcontext:*"

zstyle -s ":prompt:X$curcontext:" fgcolor fgcolor || fgcolor=black
zstyle -s ":prompt:X$curcontext:" bgcolor bgcolor || bgcolor=214
psvar[6,8]=( "$bgcolor" "$fgcolor" "$diff" )

curcontext=":*"
unset fgcolor bgcolor
