builtin eval $SETUP

local curcontext=":*" git=( (../)#.git(N) )

if [[ -n $git ]] {
  CURRENT_GIT_BRANCH=$(git rev-parse --abbrev-ref HEAD 2> /dev/null)
} else {
  CURRENT_GIT_BRANCH=''
}

[[ $CURRENT_GIT_BRANCH == HEAD ]] && CURRENT_GIT_BRANCH=$(git describe --all --exact-match HEAD 2> /dev/null)

if [[ $CURRENT_GIT_BRANCH ]] {
  local fgcolour bgcolour branch symbol
  curcontext=":git:${(b)CURRENT_GIT_BRANCH}"
  git diff --quiet &>/dev/null
  if (( $? )) { curcontext="$curcontext:dirty"
  } else { curcontext="$curcontext:clean" }
  curcontext="${curcontext}:*"

  zstyle -s ":prompt:X$curcontext:"        fgcolour   fgcolour || fgcolour=white
  zstyle -s ":prompt:X$curcontext:"        bgcolour   bgcolour || bgcolour=238
  zstyle -s ":prompt:X$curcontext:"        symbol     symbol   || symbol=''
  zstyle -s ":prompt:X$curcontext:branch"  icon       branch   || branch='î‚ '

  psvar[1,5]=( "$bgcolour" "$fgcolour" "${branch:+$branch }" "$CURRENT_GIT_BRANCH" "$symbol" )

  curcontext=":*"
  unset fgcolour bgcolour branch symbol
} else { psvar[1,5]=( "" "" "" "" "" ) }

local fgcolour bgcolour
local -F 2 diff=$elapsed[-1]
curcontext=":time"
curcontext="$curcontext:*"

zstyle -s ":prompt:X$curcontext:" fgcolour fgcolour || fgcolour=black
zstyle -s ":prompt:X$curcontext:" bgcolour bgcolour || bgcolour=214
psvar[6,8]=( "$bgcolour" "$fgcolour" "$diff" )

curcontext=":*"
unset fgcolour bgcolour
