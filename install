#!/bin/zsh -f
zmodload zsh/system
error=0
DIR=$ZSH_SCRIPT:a:h
NAME=$ZSH_SCRIPT:t
TRAPEXIT() {
  local out=$?
  printf '\nYou can run this script again by running: %s\n' "$DIR/$NAME"
  exit $out
}
trap 'exit $?' INT ERR QUIT TERM

read -r '?Would you like to enter setup? (yes/no): '
if [[ $REPLY != yes ]] return;

REPLY=$DIR
vared -p 'Install path: ' REPLY;
while [[ -e $REPLY:a && $REPLY:a != $DIR ]] {
  syserror -p "$NAME: $REPLY:a: " EEXIST;
  vared -p 'Install path: ' REPLY;
}
if [[ $REPLY:a != $DIR ]] mv -- "$DIR" "$REPLY:a";
DIR=$REPLY:a;

infile="$DIR/.zshenv" outfile="$HOME/.zshenv"
printf 'Linking %s to %s\n' "$outfile" "$infile"
if [[ $outfile:P == $infile:P ]] {
  printf '%s already linked to %s\n' "$outfile" "$infile"
} elif [[ -e $outfile ]] {
  syserror -p "$NAME: $outfile: " EEXIST
  return 1
} else {
  /bin/ln -s "$infile" "$outfile"
}

outfile="$DIR/config.zsh"
let SAVE_HIST=10000000 HIST_SIZE=100000
vared -p 'Maximum entries in history file (SAVEHIST): ' SAVE_HIST
vared -p 'Maximum history entries in memory (HISTSIZE): ' HIST_SIZE

if { whence brew >/dev/null; } {
  eval "$(trap -; brew shellenv zsh)";
} else {
  valid_prefixes=( /usr/local /opt/homebrew /home/linuxbrew/.linuxbrew )
  for valid_prefix ( $valid_prefixes ) {
    if [[ -e $valid_prefix ]] {
      HOMEBREW_PREFERRED=$valid_prefix
    }
  }
  export HOMEBREW_PREFIX="${HOMEBREW_PREFIX:-$HOMEBREW_PREFERRED}"
  export HOMEBREW_CELLAR="${HOMEBREW_CELLAR:-$HOMEBREW_PREFIX/Cellar}"
  export HOMEBREW_REPOSITORY="${HOMEBREW_REPOSITORY:-$HOMEBREW_PREFIX}"
}
OLD_PREFIX=$HOMEBREW_PREFIX
vared -p 'Homebrew prefix (HOMEBREW_PREFIX): ' HOMEBREW_PREFIX
if [[ $HOMEBREW_PREFIX != $OLD_PREFIX ]] {
  if [[ $HOMEBREW_CELLAR == $OLD_PREFIX(/*|) ]] {
    HOMEBREW_CELLAR=${HOMEBREW_CELLAR/$OLD_PREFIX/$HOMEBREW_PREFIX}
  }
  if [[ $HOMEBREW_REPOSITORY == $OLD_PREFIX(/*|) ]] {
    HOMEBREW_REPOSITORY=${HOMEBREW_REPOSITORY/$OLD_PREFIX/$HOMEBREW_PREFIX}
  }
}
vared -p 'Homebrew cellar (HOMEBREW_CELLAR): ' HOMEBREW_CELLAR
vared -p 'Homebrew repository (HOMEBREW_REPOSITORY): ' HOMEBREW_REPOSITORY

printf 'Writing config to %s\n' "$outfile"
printf '' > $outfile
printf 'SAVEHIST=%s HISTSIZE=%s\n' $SAVE_HIST $HIST_SIZE >> $outfile >&1
printf 'export HOMEBREW_PREFIX=%s\n' ${(qq)HOMEBREW_PREFIX} >> $outfile >&1
printf 'export HOMEBREW_CELLAR=%s\n' ${(qq)HOMEBREW_CELLAR} >> $outfile >&1
printf 'export HOMEBREW_REPOSITORY=%s\n' ${(qq)HOMEBREW_REPOSITORY} >> $outfile >&1

typeset -A links=(
  [zcp]=$HOMEBREW_PREFIX/share/zsh/functions/zmv
  [zln]=$HOMEBREW_PREFIX/share/zsh/functions/zmv
)
for outfile infile ( ${(@kv)links} ) {
  outfile=$DIR/functions/ln/$outfile
  printf 'Linking %s to %s\n' "$outfile" "$infile"
  /bin/rm $outfile
  /bin/ln -s $infile $outfile
}
for infile ( $DIR/crontab/* ) {
  read -r '?Install '$infile:t' to crontab? (yes/no): '
  if [[ $REPLY == yes ]] {
    ( /usr/bin/crontab -l; < $infile ) | /usr/bin/crontab
  }
}
printf '\n%s' 'Install complete!'
return error
