#!/bin/zsh -f
zmodload zsh/system
DIR=$ZSH_SCRIPT:a:h
NAME=$ZSH_SCRIPT:t
TRAPEXIT() {
  local out=$?
  printf '\nYou can run this script again by running: %s\n' "$DIR/$NAME"
  exit $out
}
trap 'exit $?' INT ERR QUIT TERM

read -r "?Would you like to enter setup? (yes/no): "
if [[ $REPLY != yes ]] return;

REPLY=$DIR
vared -p 'Install path: ' REPLY;
while [[ -e $REPLY:a && $REPLY:a != $DIR ]] {
  syserror -p "$NAME: $REPLY:a: " EEXIST;
  vared -p 'Install path: ' REPLY;
}
if [[ $REPLY:a != $DIR ]] mv -- "$DIR" "$REPLY:a";
DIR=$REPLY:a;

outfile="$HOME/.zshenv"
printf 'Linking %s to %s\n' "$DIR/.zshenv" "$outfile"
if [[ -e $outfile ]] {
  syserror -p "$NAME: $outfile: " EEXIST
  return 1
}
ln -s "$DIR/.zshenv" "$outfile"

outfile="$DIR/config.zsh"
let SAVE_HIST=10000000 HIST_SIZE=100000
vared -p 'Maximum entries in history file (SAVEHIST): ' SAVE_HIST
vared -p 'Maximum history entries in memory (HISTSIZE): ' HIST_SIZE
printf 'Writing config to %s\n' "$outfile"
printf 'SAVEHIST=%s HISTSIZE=%s\n' $SAVE_HIST $HIST_SIZE > $outfile
